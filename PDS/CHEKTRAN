CHEKTRAN START                                                          00001
*                                                                       00002
*                                                                       00003
* ASSEMBLER SUB-PROGRAMME TO SET UP AMOUNTS FOR PRINTING CHEQUES.       00004
*                                                                       00005
*  BOTH AN EDITED NUMERIC FIELD WITH THE CURRENCY SYMBOL PROTECTIVELY   00006
*  PLACED AND THE AMOUNT IN WORDS ARE PREPARED.                         00007
*                                                                       00008
*                                                                       00009
* THE FOLLOWING COMMENTS SHOW HOW PART OF THE WORKING STORAGE SECTION   00010
* OF THE DATA DIVISION OF A COBOL CALLING PROGRAMME SHOULD BE SET UP.   00011
* THIS IS THE PART OF THE CALLING PROGRAMME'S STORAGE THAT MUST BE      00012
* AVAILABLE TO THIS ROUTINE.                                            00013
*                                                                       00014
*  NOTE THAT THIS ROUTINE DISTURBS ONLY THE 'CKT-OUTPUT' GROUP ITEM.    00015
*  ALSO NOTE THAT THE SYMBOLS 'ABC', 'DEF', 'G', 'HIJ', AND 'N' ARE     00016
*  INTENDED HERE AS REPRESENTATIONAL VARIABLES.                         00017
*                                                                       00018
*      05  CHEKTRAN-DATA.                                               00019
*          10  CKT-CODE                            PICTURE  X(1).       00020
*          10  CKT-A-TOTAL-L   VALUE ABC                                00021
*                              COMPUTATIONAL-3     PICTURE  9(3).       00022
*          10  CKT-A-LINE-L    VALUE DEF                                00023
*                              COMPUTATIONAL-3     PICTURE  9(3).       00024
*          10  CKT-A-INDENT    VALUE G                                  00025
*                              COMPUTATIONAL-3     PICTURE  9(1).       00026
*          10  CKT-DOLLARS     COMPUTATIONAL-3     PICTURE S9(9)V9(2).  00027
*          10  CKT-PENCE REDEFINES CKT-DOLLARS                          00028
*                              COMPUTATIONAL-3     PICTURE S9(11).      00029
*          10  CKT-OUTPUT.                                              00030
*              15  CKT-A-LINES COMPUTATIONAL-3     PICTURE S9(1).       00031
*              15  CKT-NUMERIC                     PICTURE  X(16).      00032
*              15  CKT-ALPHABETIC.                                      00033
*                  20  CKT-LINE-1                  PICTURE  X(DEF).     00034
*                  20  CKT LINE-2                  PICTURE  X(DEF).     00035
*                  .            .                                       00036
*                  .            .                                       00037
*                  20  CKT-LINE-N                  PICTURE  X(HIJ).     00038
*                                                                       00039
*      NOTE THAT 'HIJ' MUST NOT EXCEED 'DEF' AND THAT 'ABC' MUST EQUAL  00040
*           (N MINUS ONE) TIMES 'DEF' PLUS 'HIJ'.                       00041
*                                                                       00042
*                                                                       00043
* THE INPUT CODING HAS THE FOLLOWING MEANING -                          00044
*                                                                       00045
*   CKT-CODE      - LETTER 'F' SIGNIFIES FRENCH LANGUAGE, DOLLARS AND   00046
*                   CENTS.                                              00047
*                                                                       00048
*                 - LETTER 'D' SIGNIFIES ENGLISH LANGUAGE, DECIMAL      00049
*                   STERLING.                                           00050
*                                                                       00051
*                 - LETTER 'L' SIGNIFIES ENGLISH LANGUAGE, L-S-D        00052
*                   STERLING.                                           00053
*                                                                       00054
*                 - ANY OTHER CODING SIGNIFIES ENGLISH LANGUAGE,        00055
*                   DOLLARS AND CENTS.                                  00056
*                                                                       00057
*   CKT-A-TOTAL-L - THE VALUE 'ABC' IS THE NUMBER OF CHARACTERS IN THE  00058
*                   'CKT-ALPHABETIC' OUTPUT AREA. THIS MUST BE IN THE   00059
*                   RANGE 30-259 INCLUSIVE.                             00060
*                                                                       00061
*   CKT-A-LINE-L  - THE VALUE 'DEF' IS THE NUMBER OF CHARACTERS IN EACH 00062
*                   LINE OF THE ALPHABETIC OUTPUT AREA, WITH THE        00063
*                   EXCEPTION THAT THE FINAL LINE MAY BE SHORTER.       00064
*                   THE VALUE 'DEF' MUST NOT BE LESS THAN 30.           00065
*                                                                       00066
*   CKT-INDENT    - THE VALUE 'G' IS THE NUMBER OF BLANK SPACES         00067
*                   INSERTED AT THE BEGINNING OF EACH ALPHABETIC OUTPUT 00068
*                   LINE BUT THE FIRST. IT MUST NOT BE NEGATIVE.        00069
*                   EITHER 0 OR 3 IS SUGGESTED AS A VALUE.              00070
*                                                                       00071
*                                                                       00072
*   THE AMOUNT FIELD MUST BE POSITIVE. IT MUST CONTAIN THE NUMBER OF    00073
*   MINOR UNITS OF CURRENCY IN THE AMOUNT.                              00074
*                                                                       00075
*   THIS ROUTINE CAN BE ENTERED VIA A STATEMENT SUCH AS -               00076
*        CALL 'CHEKTRAN' USING CHEKTRAN-DATA.                           00077
*                                                                       00078
*                                                                       00079
* OUTPUT FROM THE PROGRAMME IS SET UP IN THE FOLLOWING 3 AREAS -        00080
*                                                                       00081
*   CKT-A-LINES    - THIS ONE BYTE PACKED DECIMAL FIELD GIVES THE       00082
*                    NUMBER OF LINES OF ALPHABETIC DATA SET UP TO       00083
*                    FULLY DESCRIBE THE AMOUNT IN WORDS.                00084
*                    THIS FIELD IS SET TO ZERO SHOULD ANY INVALIDITIES  00085
*                    BE DETECTED BY THIS PROGRAMME.                     00086
*                                                                       00087
*   CKT-NUMERIC    - THIS 16 CHARACTER FIELD IS SET UP WITH THE CHEQUE  00088
*                    AMOUNT EDITED, AND WITH THE CURRENCY SYMBOL JUST   00089
*                    TO THE LEFT OF THE MOST SIGNIFICANT DIGIT OF THE   00090
*                    AMOUNT.                                            00091
*                                                                       00092
*   CKT-ALPHABETIC - THIS AREA IS SET UP WITH THE AMOUNT IN WORDS.      00093
*                    NOTE THAT BOTH ENDS OF THE ALPHABETIC DESCRIPTION  00094
*                    ARE AUTOMATICALLY BRACKETED BY A PAIR OF           00095
*                    ASTERISKS, AND ALSO THAT UNUSED SPACE IN THE RIGHT 00096
*                    HAND END OF THIS AREA IS BLANKED.                  00097
*                                                                       00098
*                                                                       00099
* INVALIDITIES CAN BE TURNED UP FOR THE FOLLOWING REASONS -             00100
*                                                                       00101
*   - 'CKT-A-TOTAL-L' IS NOT IN THE RANGE 30 TO 259 INCLUSIVE.          00102
*                                                                       00103
*   - 'CKT-A-LINE-L' IS LESS THAN 30.                                   00104
*                                                                       00105
*   - 'CKT-A-INDENT' IS NEGATIVE.                                       00106
*                                                                       00107
*   - THE AMOUNT FIELD IS ZERO OR NEGATIVE.                             00108
*                                                                       00109
*   - THE 'CKT-NUMERIC' FIELD IS NOT LARGE ENOUGH TO HOLD SOME          00110
*     EXTREMELY HIGH L-S-D STERLING AMOUNTS.                            00111
*                                                                       00112
*   - THE 'CKT-ALPHABETIC' IS NOT LARGE ENOUGH TO HOLD THE ENTIRE       00113
*     ALPHABETIC DESCRIPTION OF THE AMOUNT.                             00114
*                                                                       00115
*   AGAIN NOTE THAT 'CKT-A-LINES' IS SET TO ZERO ON ALL INVALIDITIES.   00116
*   THE CONTENTS OF THE REST OF THE OUTPUT AREA IS UNPREDICTABLE.       00117
*                                                                       00118
*                                                                       00119
*                                                                       00120
RB       EQU   2                        BASE REGISTER.                  00121
RC       EQU   3                        CURRENCY PHRASE POINTER.        00122
RG       EQU   4                        POINTER TO PHRASES BEING GOTTEN 00123
RI       EQU   5                        INPUT/OUTPUT AREA POINTER.      00124
RL       EQU   6                        LANGUAGE PHRASE POINTER.        00125
RN       EQU   7                        NEXT LINE START POINTER.        00126
RP       EQU   8                        OUTPUT PLACEMENT POINTER.       00127
RW1      EQU   9                        PRIMARY WORK REGISTER.          00128
RW2      EQU   10                       SECONDARY WORK REGISTER.        00129
RX1      EQU   11                       HIGH LEVEL LINKAGE REGISTER.    00130
RX2      EQU   12                       LOW LEVEL LINKAGE REGISTER.     00131
*                                                                       00132
         SAVE  (14,12)                  SAVE REGISTERS.                 00133
         BALR  RB,0                     SET UP THE BASE REGISTER.       00134
         USING *,RB                                                     00135
         USING LANGGO,RL                                                00136
         USING CURRGO,RC                                                00137
         USING INANDOUT,RI                                              00138
         L     RI,0(0,1)                SET UP I/O LINKAGE POINTER.     00139
         LA    RP,ALFOUT+1              INITIALIZE 'RP'.                00140
         ZAP   DOUBLEWD,CHAROUT                                         00141
         CVB   RW1,DOUBLEWD             VERIFY THAT THE NUMBER OF       00142
         CH    RW1,BINH30               CHARACTERS IN THE ALPHABETIC    00143
         BL    INVALID                  OUTPUT MESSAGE IS IN THE RANGE  00144
         CH    RW1,BINH259              30-259 INCLUSIVE.               00145
         BH    INVALID                                                  00146
         SH    RW1,BINARYH4             SET UP LENGTH OF 'INIZEALF'.    00147
         STC   RW1,INIZEALF+1                                           00148
         LA    RW2,ALFOUT+3             SET UP 'PUTLIMIT' WITH ADDRESS  00149
         AR    RW2,RW1                  OF RIGHTMOST VALID PLACEMENT    00150
         ST    RW2,PUTLIMIT             SPOT.                           00151
         ZAP   DOUBLEWD,CHARPLN                                         00152
         CVB   RN,DOUBLEWD              VERIFY THAT THERE ARE 30 OR     00153
         CH    RN,BINH30                MORE CHARACTERS PER LINE.       00154
         BL    INVALID                                                  00155
         STH   RN,HCHARPLN              SET UP 'HCHARPLN' AND           00156
         BCTR  RN,0                     INITIALIZE REGISTER 'RN'.       00157
         AR    RN,RP                                                    00158
         ZAP   WORK(1),INDENT           SET UP 'HINDENT' AND IN THE     00159
         BM    INVALID                  PROCESS ENSURE THAT THE LEFT    00160
         MVO   HLENGTH(3),WORK(1)       HAND BYTE OF 'HLENGTH' IS ZERO. 00161
         MVC   HINDENT,HLENGTH                                          00162
*                                                                       00163
         ZAP   DOUBLEWD(7),AMOUNT       SET UP AND TEST THE AMOUNT.     00164
         BNH   INVALID                                                  00165
         MVC   LINESOUT(20),PACKED1     INITIALIZE FIRST 20 BYTES OF    00166
         LA    1,NUMOUT+12              OUTPUT AND REGISTER 1.          00167
         LA    RC,DOLLAR                SET UP 'RC' RE DOLLARS & CENTS. 00168
         CLI   INCODE,C'F'                                              00169
         BNE   *+12                     FIX 'RL' RE LANGUAGE.           00170
         LA    RL,FRENCH-1                                              00171
         B     DECIMAL                                                  00172
         LA    RL,ENGLISH-1                                             00173
         CLI   INCODE,C'D'                                              00174
         BNE   *+12                                                     00175
         LA    RC,POUNDDEC              SET UP DECIMAL POUNDS.          00176
         B     DECIMAL                                                  00177
         CLI   INCODE,C'L'                                              00178
         BE    LSDDO                                                    00179
DECIMAL  EDMK  NUMOUT(16),DOUBLEWD+1    FOR ALL DECIMAL CURRENCIES, SET 00180
         BCTR  1,0                      UP THE NUMERIC OUTPUT FIELD     00181
         MVC   0(1,1),CURRSYMB          WITH THE CURRENCY SYMBOL AND    00182
         MVO   DOUBLEWD,DOUBLEWD+1(6)   SHIFT THE AMOUNT PROPERLY IN    00183
         B     INIZEALF                 THE DOUBLEWORD FIELD.           00184
LSDDO    LA    RC,POUNDLSD                                              00185
         ZAP   WORK(9),DOUBLEWD+1(6)                                    00186
         DP    WORK(9),LSDP240          L-S-D STERLING -                00187
         DP    WORK+5(4),LSDP12                                         00188
         MVN   WORK+4(3),WORK+5         FIX UP REGISTER 'RC'.           00189
         MVC   WORK+5(1),WORK+6         FIND THE NUMBER OF POUNDS,      00190
         MVC   WORK+6(1),WORK+8         SHILLINGS, AND PENCE.           00191
         MVC   NUMOUT+13(9),LSDEDIT     SET UP FOR THE EDIT.            00192
         EDMK  NUMOUT(19),WORK          DO THE EDIT AND PLACE THE       00193
         BCTR  1,0                      CURRENCY SYMBOL.                00194
         MVC   0(1,1),CURRSYMB          CLOSE UP THE EDITED FIELD IF    00195
         CLI   NUMOUT+17,C'0'           THERE ARE ZEROES IN EITHER THE  00196
         BNE   *+10                     TENS SPOT OF SHILLINGS OR THE   00197
         MVC   NUMOUT+17(4),NUMOUT+18   TENS SPOT OF THE PENCE.         00198
         CLI   NUMOUT+14,C'0'                                           00199
         BNE   *+10                     SHIFT THE FIELD LEFT UNTIL IT   00200
         MVC   NUMOUT+14(7),NUMOUT+15   IS PROPERLY ALIGNED, ALWAYS     00201
LSDNFIX  CLI   NUMOUT,C' '              ENSURING THAT NO SIGNIFICANT    00202
         BNE   INVALID                  DATA IS SHIFTED OUT AT THE      00203
         MVC   NUMOUT(21),NUMOUT+1      TOP END.                        00204
         CLI   NUMOUT+16,C'*'                                           00205
         BNE   LSDNFIX                  SET UP THE AMOUNT IN 'DOUBLEWD' 00206
         MVO   WORK(8),WORK(7)          PROPERLY. SHILLINGS IN TOP      00207
         MVC   DOUBLEWD(1),WORK+5       SPOT, POUNDS IN NEXT FIVE, AND  00208
         MVC   DOUBLEWD+1(5),WORK       PENCE IN THE NEXT.              00209
         MVC   DOUBLEWD+6(1),WORK+6                                     00210
*                                                                       00211
INIZEALF MVC   ALFOUT+4(0),ALFOUT+3     BLANK ALPHA FIELD.              00212
         MVC   FRFIXPOS,FRFIXSNG                                        00213
         CLC   DOUBLEWD+1(2),HEXTEST    DO THE MILLIONS IF THEIR DIGITS 00214
         BE    DOTHOU                   ARE NON-ZERO.                   00215
         MVC   HTU,DOUBLEWD+1                                           00216
         BAL   RX1,DOHTU                                                00217
         LA    RG,MILLIONS              ONLY USE THE PLURAL ON FRENCH.  00218
         CLC   HTU,HEXTEST+4                                            00219
         BNE   *+8                                                      00220
         LA    RG,MILLION                                               00221
         BAL   RX2,PUT                                                  00222
DOTHOU   MVO   HTU(3),DOUBLEWD+3(2)     NOW DO THE THOUSANDS IF THEIR   00223
         CLC   HTU,HEXTEST              DIGITS ARE NON-ZERO.            00224
         BE    DOUNITS                                                  00225
         CLI   INCODE,C'F'              AVOID DIGIT WORDS IF ONE        00226
         BNE   *+14                     THOUSAND IN FRENCH.             00227
         CLC   HTU,HEXTEST+4                                            00228
         BE    *+8                                                      00229
         BAL   RX1,DOHTU                                                00230
         LA    RG,THOUSAND                                              00231
         BAL   RX2,PUT                                                  00232
DOUNITS  MVC   FRFIXPOS,FRFIXPLR        NOW DO THE HUNDREDS, TENS, AND  00233
         LA    RX1,DOMAJOR              UNITS DIGITS.                   00234
         MVC   HTU,DOUBLEWD+4                                           00235
         NI    H,X'0F'                                                  00236
         CLI   H,X'00'                                                  00237
         BNE   DOHTU                                                    00238
         CLC   DOUBLEWD+1(4),HEXTEST                                    00239
         BNE   DOANDTU                                                  00240
         CLI   TU,X'00'                                                 00241
         BNE   DOTU                                                     00242
DOMAJOR  LA    RG,MAJORS                NOW DESCRIBE ANY MAJOR UNITS.   00243
         CLC   DOUBLEWD+1(5),HEXTEST+1                                  00244
         BH    FIXMAJOR                                                 00245
         BL    TRYINTER                                                 00246
         LA    RG,MAJOR                                                 00247
FIXMAJOR BAL   RX2,PUT                                                  00248
TRYINTER CLI   DOUBLEWD,X'00'           NOW DESCRIBE ANY INTERMEDIATE   00249
         BE    TRYMINOR                 UNITS.                          00250
         MVC   TU,DOUBLEWD                                              00251
         BAL   RX1,DOTU                                                 00252
         LA    RG,INTERS                                                00253
         CLI   TU,X'01'                                                 00254
         BNE   FIXINTER                                                 00255
         LA    RG,INTER                                                 00256
FIXINTER BAL   RX2,PUT                                                  00257
TRYMINOR LA    RG,ONLY                  NOW DESCRIBE ANY MINOR UNITS.   00258
         CLI   DOUBLEWD+6,X'00'                                         00259
         BE    FIXMINOR                                                 00260
         MVC   TU,DOUBLEWD+6                                            00261
         CLC   DOUBLEWD(6),HEXTEST                                      00262
         BL    *+12                                                     00263
         LA    RG,AND                                                   00264
         BAL   RX2,PUT                                                  00265
         BAL   RX1,DOTU                                                 00266
         LA    RG,MINORS                                                00267
         CLI   TU,X'01'                                                 00268
         BNE   FIXMINOR                                                 00269
         LA    RG,MINOR                                                 00270
FIXMINOR BAL   RX2,PUT                                                  00271
OUT      RETURN (14,12)                                                 00272
INVALID  MVI   LINESOUT,X'0C'                                           00273
         B     OUT                                                      00274
*                                                                       00275
         DS    0D                                                       00276
DOUBLEWD DS    D                                                        00277
PUTLIMIT DS    F                        ADDRESS OF LAST ALLOWABLE SPOT. 00278
HCHARPLN DS    H                                                        00279
HINDENT  DS    H                                                        00280
BINARYH2 DC    X'0002'                                                  00281
BINARYH4 DC    X'0004'                                                  00282
BINH30   DC    X'001E'                                                  00283
BINH259  DC    X'0103'                                                  00284
HLENGTH  DS    H                                                        00285
WORK     DS    CL22                                                     00286
PACKED1  DC    X'1C'                                                    00287
         DC    X'40402020206B2020206B2021204B2020',C'** '               00288
LSDEDIT  DC    X'602020602020',C'** '                                   00289
LSDP12   DC    X'012C'                                                  00290
HEXTEST  DC    X'000000000001'                                          00291
*                                                                       00292
*                                                                       00293
* SUBROUTINE TO PLACE PHRASES FOR HUNDREDS DIGIT AND TENS AND UNITS     00294
* DIGITS.                                                               00295
*                                                                       00296
*  ENTRY SHOULD BE MADE SO THAT REGISTER 'RX1' IS SET UP FOR RETURN.    00297
*                                                                       00298
*  FOR ALL ENTRIES, THE POSITION 'TU' MUST HAVE BEEN SET UP WITH THE    00299
*  TENS DIGIT IN THE ZONE PORTION AND THE UNITS DIGIT IN THE NUMERIC    00300
*  PORTION.                                                             00301
*                                                                       00302
*  IF ENTRY IS MADE TO 'DOHTU', THE POSITION 'H' MUST HAVE BEEN SET UP  00303
*  WITH ZERO IN THE ZONE PORTION AND THE HUNDREDS DIGIT IN THE NUMERIC  00304
*  PORTION. THE ROUTINE WILL PLACE THE HUNDREDS DIGIT AND WORD IF       00305
*  NEEDED, THE CONNECTIVE 'AND' WHERE REQUIRED, AND THE TENS AND UNITS  00306
*  DIGITS WHEN REQUIRED. DO NOT ENTER IF ALL THE DIGITS ARE ZERO.       00307
*                                                                       00308
*  IF ENTRY IS MADE TO 'DOANDTU', THE ROUTINE WILL EXIT AFTER DOING     00309
*  NOTHING IF 'TU' IS ZERO. OTHERWISE THE ROUTINE WILL PLACE THE        00310
*  CONNECTIVE 'AND' IF THE LANGUAGE IS ENGLISH AND THEN ALWAYS PLACE    00311
*  THE TENS AND UNITS DIGITS.                                           00312
*                                                                       00313
*  IF ENTRY IS MADE TO 'DOTU', THE ROUTINE WILL PLACE THE TENS AND      00314
*  UNITS DIGITS.                                                        00315
*                                                                       00316
DOHTU    CLI   H,X'00'                  GO DIRECTLY TO 'DOTU' IF THE    00317
         BE    DOTU                     HUNDREDS DIGIT IS ZERO.         00318
         MVC   HTUFIX+5(1),H                                            00319
HTUFIX   MVC   HTULOAD+3(1),0(RL)       SET UP 'RG' TO ACCESS H PHRASE. 00320
HTULOAD  LA    RG,0(0,RL)                                               00321
         CLI   1(RG),C'U'               AVOID DIGIT IF FRENCH ONE, ALSO 00322
         BE    HTUDO100                 AVOID SPECIAL PLURAL OF 100.    00323
         BAL   RX2,PUT                  PLACE HUNDREDS DIGIT. AVOID     00324
         CLI   TU,X'00'                 SPECIAL PLURAL OF 100 IF 'TU'   00325
         BE    HTU100S                  IS NOT ZERO.                    00326
HTUDO100 LA    RG,HUNDRED               PLACE REGULAR 100 PHRASE.       00327
         BAL   RX2,PUT                                                  00328
DOANDTU  CLI   TU,X'00'                 EXIT IF 'TU' IS ZERO.           00329
         BCR   8,RX1                                                    00330
         CLI   AND+1,C'A'               PLACE 'AND' IF ENGLISH.         00331
         BNE   DOTU                                                     00332
         LA    RG,AND                   ALWAYS GO TO 'DOTU'.            00333
         BAL   RX2,PUT                                                  00334
         B     DOTU                                                     00335
HTU100S  MVC   PUTLADRS+3(1),DISP100S   PLACE APPROPRIATE HUNDRED       00336
         BAL   RX2,PUTLADRS             PHRASE.                         00337
         BR    RX1                      EXIT, AS 'TU' EQUALS ZERO.      00338
*                                                                       00339
DOTU     MVC   PUTLDISP+5(1),TU         DO NUMBERS 01-19 DIRECTLY VIA   00340
         CLI   TU,X'19'                 POSITION TU.                    00341
         BNH   TUFINAL                                                  00342
         MVZ   TUFIXT+3(1),TU           SET UP 'RW1' WITH THE ADDRESS   00343
TUFIXT   LA    RW1,0(0,RL)              OF POSITION T0.                 00344
         MVZ   TUFIXU+5(1),2(RW1)       ALSO SET UP ZONE OF POSITION T2 00345
         MVN   TUFIXU+5(1),TU           AND NUMERIC OF U IN 'TUFIXU+5'. 00346
         CLI   TUFIXU+5,X'00'           IF 'TUFIXU+5' IS ZERO, DO       00347
         BE    TUFINAL                  DIRECTLY VIA POSITION TU.       00348
         MVC   TULOADT+3(1),1(RW1)      SET UP 'RG' TO ACCESS TENS      00349
TULOADT  LA    RG,0(0,RL)               PHRASE VIA POSITION T1.         00350
         TM    2(RW1),X'0F'             USE A CONNECTIVE HYPHEN IF THE  00351
         BZ    TUDOHYPH                 NUMERIC PART OF POSITION T2 IS  00352
         MVZ   TUTESTU+1(1),TU          ZERO OR IF THE UNITS DIGIT IS   00353
TUTESTU  CLI   TU,X'01'                 NOT ONE.                        00354
         BNE   TUDOHYPH                                                 00355
         BAL   RX2,PUT                  NO HYPHEN CASE - PLACE THE TENS 00356
         LA    RG,AND                   PHRASE, THE 'AND', AND THEN SET 00357
         BAL   RX2,PUT                  UP FOR THE FINAL PLACEMENT OF   00358
         MVC   PUTLDISP+5(1),TUFIXU+5   THE UNITS PHRASE.               00359
TUFINAL  BAL   RX2,PUTLDISP             PLACE THE ONE PHRASE OR THE     00360
         BR    RX1                      FINAL PHRASE AND EXIT.          00361
TUDOHYPH LA    RW1,HLENGTH+1                                            00362
         LH    RW2,BINARYH2             HYPHEN CASE - SET UP THE TENS   00363
         BAL   RX2,TUHYPHSR             PHRASE IN THE WORK AREA,        00364
         AR    RW1,RW2                  APPEND THE HYPHEN,              00365
         MVI   0(RW1),C'-'              AND THEN APPEND THE PROPER      00366
TUFIXU   MVC   TULOADU+3(1),0(RL)       UNITS PHRASE.                   00367
TULOADU  LA    RG,0(0,RL)                                               00368
         BAL   RX2,TUHYPHSR             FINALLY PLACE THE COMPOSITE     00369
         STH   RW2,HLENGTH              PHRASE INTO THE OUTPUT AREA     00370
         LA    RG,HLENGTH+1             FROM THE WORK AREA.             00371
         BAL   RX2,PUT                                                  00372
         BR    RX1                                                      00373
TUHYPHSR MVC   TUMOVE+1(1),0(RG)                                        00374
         MVC   HLENGTH+1(1),0(RG)                                       00375
TUMOVE   MVC   1(0,RW1),1(RG)                                           00376
         AH    RW2,HLENGTH                                              00377
         BR    RX2                                                      00378
*                                                                       00379
*                                                                       00380
*                                                                       00381
* SUBROUTINE TO PLACE A PHRASE IN THE OUTPUT AREA.                      00382
*                                                                       00383
*  ENTRY SHOULD BE MADE SO THAT REGISTER 'RX2' IS SET UP FOR RETURN.    00384
*                                                                       00385
*  IF ENTRY IS MADE TO 'PUTLDISP', THE SINGLE POSITION AT THE ADDRESS   00386
*  'PUTLDISP+5' MUST HAVE BEEN SET UP WITH THE HEXADECIMAL POSITION OF  00387
*  THE LANGUAGE TABLE AT WHICH THE PROPER 'DISPLACEMENT' WITHIN THE     00388
*  SAME TABLE IS FOUND.                                                 00389
*                                                                       00390
*  IF ENTRY IS MADE TO 'PUTLADRS', THE SINGLE POSITION AT THE ADDRESS   00391
*  'PUTLADRS+3' MUST HAVE BEEN SET UP WITH THE 'DISPLACEMENT' WITHIN    00392
*  THE LANGUAGE TABLE AT WHICH THE PHRASE TO BE PLACED IS DEFINED.      00393
*                                                                       00394
*  IF ENTRY IS MADE TO 'PUT', REGISTER 'RG' MUST HAVE BEEN SET UP WITH  00395
*  THE ADDRESS AT WHICH THE PHRASE IS DEFINED. AT THIS SPOT, THE LENGTH 00396
*  OF THE PHRASE MINUS ONE HAS BEEN SET UP. THE PHRASE ITSELF FOLLOWS.  00397
*                                                                       00398
*   NOTE THAT AFTER THE PLACEMENT HAS BEEN DONE, REGISTER 'RP' POINTS   00399
*   TO THE RIGHTMOST CHARACTER OF THE PHRASE JUST MOVED IN.             00400
*                                                                       00401
*   ALSO NOTE THAT DIRECT EXIT TO 'INVALID' WILL BE MADE IF PLACEMENT   00402
*   OF THE PHRASE WOULD OVERFLOW THE OUTPUT AREA.                       00403
*                                                                       00404
PUTLDISP MVC   PUTLADRS+3(1),0(RL)      MOVE IN THE DISPLACEMENT.       00405
PUTLADRS LA    RG,0(0,RL)               SET UP LANGUAGE PHRASE ADDRESS. 00406
PUT      AH    RP,BINARYH2              SET UP 'RP' WITH PROBABLE NEXT  00407
         MVC   HLENGTH+1(1),0(RG)       PLACEMENT SPOT.                 00408
         MVC   PUTMOVE+1(1),0(RG)       SET UP THE LENGTH IN TWO SPOTS. 00409
PUTSETUP LR    RW1,RP                   'RW1' GETS FIRST PLACEMENT SPOT 00410
         AH    RP,HLENGTH               AND 'RP' GETS LAST ONE.         00411
         CLR   RP,RN                    SEE IF A NEW LINE MUST BE       00412
         BNL   PUTNEWLN                 STARTED.                        00413
         CL    RP,PUTLIMIT              GO TO 'INVALID' IF THE PHRASE   00414
         BH    INVALID                  AT HAND WONT FIT IN THE AREA.   00415
PUTMOVE  MVC   0(0,RW1),1(RG)           MOVE THE PHRASE INTO THE OUTPUT 00416
         BR    RX2                      AREA AND EXIT.                  00417
PUTNEWLN LR    RP,RN                    TO SET UP A NEW LINE. FIX 'RP', 00418
         AH    RN,HCHARPLN              CRANK 'RN', ADD THE INDENTATION 00419
         AH    RP,HINDENT               TO THE NEW VALUE OF 'RP', ADD   00420
         AP    LINESOUT,PACKED1         ONE TO THE OUTPUT LINE COUNT,   00421
         B     PUTSETUP                 AND RETURN TO 'PUTSETUP'.       00422
*                                                                       00423
*                                                                       00424
*                                                                       00425
*                                                                       00426
* THE ITEMS THAT VARY BY LANGUAGE ARE DESCRIBED AND DEFINED BELOW.      00427
*                                                                       00428
*  REGISTER 'RL' MUST BE LOADED WITH ONE LESS THAN THE ADDRESS OF THE   00429
*  BEGINNING OF THE APPROPRIATE LANGUAGE TABLE.                         00430
*                                                                       00431
*  THE 'DISPLACEMENT' MENTIONED BELOW IS THE VALUE THAT MUST BE ADDED   00432
*  TO THE CONTENTS OF REGISTER 'RL' TO FIND THE ADDRESS FROM WHICH AN   00433
*  APPROPRIATE WORD OR PHRASE MAY BE OBTAINED FOR PLACEMENT IN THE      00434
*  OUTPUT STRING. AS ITS VALUE NEVER EXCEEDS 255, IT IS CONTAINED IN    00435
*  ONE BYTE.                                                            00436
*                                                                       00437
*   CONSIDER A DECIMAL NUMBER IN THE RANGE 01-99 AS BEING DESCRIBED BY  00438
*   THE TWO DECIMAL DIGITS 'TU'.                                        00439
*                                                                       00440
*    THE HEXADECIMAL CODING POSITIONS OF EACH LANGUAGE TABLE ARE USED   00441
*    AS FOLLOWS. REMEMBER THAT EACH TABLE STARTS AT POSITION 01.        00442
*                                                                       00443
*     POSITIONS 01-09, 10-19 CONTAIN THE 'DISPLACEMENT' FOR THE NUMBERS 00444
*     01-09, 10-19 RESPECTIVELY.                                        00445
*     ALSO IF THE NUMBER IS NOT 10 BUT IS OF THE FORM 'T0' AND IF       00446
*     POSITION T2 IS ZERO IN ITS ZONE PORTION, THEN POSITION T0         00447
*     CONTAINS THE REQUIRED 'DISPLACEMENT'.                             00448
*     ALL SUCH NUMBERS NEED ONLY ONE PHRASE.                            00449
*                                                                       00450
*     ALL NUMBERS NOT DEALT WITH IN THE ABOVE PARAGRAPH NEED A TENS     00451
*     PHRASE, A CONNECTIVE, AND A UNITS PHRASE.                         00452
*                                                                       00453
*      POSITION T1 CONTAINS THE 'DISPLACEMENT' NEEDED TO ACCESS THE     00454
*      TENS PHRASE.                                                     00455
*                                                                       00456
*      IF 'U' IS 1 AND THE NUMERIC PORTION OF POSITION T2 IS NON-ZERO,  00457
*      THE CONNECTIVE IS ' AND ' IN THE PROPER LANGUAGE.                00458
*      IN ALL OTHER CASES THE CONNECTIVE IS A HYPHEN.                   00459
*                                                                       00460
*      THE 'DISPLACEMENT' NEEDED TO ACCESS THE UNITS PHRASE IS FOUND IN 00461
*      POSITION ZU, WHERE 'Z' IS THE ZONE PORTION OF POSITION T2.       00462
*      NOTE THAT 'Z' CAN HAVE A VALUE OF 0 OR 1.                        00463
*                                                                       00464
*       IT SHOULD ALSO BE NOTED THAT POSITION 7F CONTAINS THE           00465
*       'DISPLACEMENT' FOR THE WORD TO FOLLOW THE HUNDREDS DIGIT IF IT  00466
*       IS NOT ONE IN FRENCH AND IF THE FOLLOWING TENS AND UNITS DIGITS 00467
*       ARE ZERO.                                                       00468
*       POSITIONS 7F AND 80 ARE VARIABLE IN THE FRENCH LANGUAGE TABLE.  00469
*                                                                       00470
*                                                                       00471
ENGLISH  DC    X'732C0A3B5B6793996B'    RE 01-09.                       00472
         DC    X'04',C'THREE'           D=10                            00473
         DC    X'9F83A3AAB377BCC4CED7'  RE 10-19.                       00474
         DC    X'04',C'FORTY'           D=26                            00475
         DC    X'E0E000'                RE 20'S.                        00476
         DC    X'07',C'THOUSAND'        D=35                            00477
         DC    X'02',C'TWO'             D=44                            00478
         DC    X'E7E700'                RE 30'S.                        00479
         DC    X'06',C'HUNDRED'         D=51                            00480
         DC    X'03',C'FOUR'            D=59                            00481
         DC    X'1A1A00'                RE 40'S.                        00482
         DC    X'05',C'EIGHTY'          D=67                            00483
         DC    X'04',C'FIFTY'           D=74                            00484
         DC    X'4A4A00'                RE 50'S.                        00485
         DC    X'06',C'ONLY **'         D=83                            00486
         DC    X'03',C'FIVE'            D=91                            00487
         DC    X'8A8A00'                RE 60'S.                        00488
         DC    X'02',C'AND'             D=99                            00489
         DC    X'02',C'SIX'             D=103                           00490
         DC    X'03',C'NINE'            D=107                           00491
         DC    X'EEEE00'                RE 70'S.                        00492
         DC    X'02',C'ONE'             D=115                           00493
         DC    X'06',C'FIFTEEN'         D=119                           00494
         DC    X'33'                    SPECIAL 100'S.                  00495
         DC    X'434300'                RE 80'S.                        00496
         DC    X'05',C'ELEVEN'          D=131                           00497
         DC    X'04',C'SIXTY'           D=138                           00498
         DC    X'F6F600'                RE 90'S.                        00499
         DC    X'04',C'SEVEN'           D=147                           00500
         DC    X'04',C'EIGHT'           D=153                           00501
         DC    X'02',C'TEN'             D=159                           00502
         DC    X'05',C'TWELVE'          D=163                           00503
         DC    X'07',C'THIRTEEN'        D=170                           00504
         DC    X'07',C'FOURTEEN'        D=179                           00505
         DC    X'06',C'SIXTEEN'         D=188                           00506
         DC    X'08',C'SEVENTEEN'       D=196                           00507
         DC    X'07',C'EIGHTEEN'        D=206                           00508
         DC    X'07',C'NINETEEN'        D=215                           00509
         DC    X'05',C'TWENTY'          D=224                           00510
         DC    X'05',C'THIRTY'          D=231                           00511
         DC    X'06',C'SEVENTY'         D=238                           00512
         DC    X'05',C'NINETY'          D=246                           00513
*                                                                       00514
LSDP240  DC    X'0000240C'                                              00515
HTU      DS    0CL2                                                     00516
H        DS    CL1                                                      00517
TU       DS    CL1                                                      00518
         DS    CL1                      FOR 'MVO' SLOP.                 00519
*                                                                       00520
MILLION  DC    X'06',C'MILLION'         D=260                           00521
*                                                                       00522
*                                                                       00523
FRENCH   DC    X'73660A298338A7ACB1'    RE 01-09.                       00524
         DC    X'04',C'TROIS'           D=10                            00525
         DC    X'3CB61ABB76C26B88C9D2'  RE 10-19.                       00526
         DC    X'04',C'DOUZE'           D=26                            00527
         DC    X'DBDB0F'                RE 20'S.                        00528
         DC    X'04',C'MILLE'           D=35                            00529
         DC    X'05',C'QUATRE'          D=41                            00530
         DC    X'E1E10F'                RE 30'S.                        00531
         DC    X'03',C'CENT'            D=51                            00532
         DC    X'02',C'SIX'             D=56                            00533
         DC    X'02',C'DIX'             D=60                            00534
         DC    X'E8E80F'                RE 40'S.                        00535
         DC    X'0B',C'QUATRE-VINGT'    D=67                            00536
         DC    X'F1F10F'                RE 50'S.                        00537
         DC    X'0B',C'SEULEMENT **'    D=83                            00538
         DC    X'FBFB0F'                RE 60'S.                        00539
         DC    X'01',C'ET'              D=99                            00540
         DC    X'03',C'DEUX'            D=102                           00541
         DC    X'04',C'SEIZE'           D=107                           00542
         DC    X'FB1F'                  RE 70'S.                        00543
         DC    X'01',C'UN'              D=115                           00544
         DC    X'07',C'QUATORZE'        D=118                           00545
FRFIXPOS DS    CL2                      SPECIAL RE 100'S AND 80.        00546
         DC    X'4300'                  RE 80'S.                        00547
         DC    X'03',C'CINQ'            D=131                           00548
         DC    X'07',C'DIX-SEPT'        D=136                           00549
         DC    X'4310'                  RE 90'S.                        00550
         DC    X'04',C'CENTS'           D=147                           00551
         DC    X'0C',C'QUATRE-VINGTS'   D=153                           00552
         DC    X'03',C'SEPT'            D=167                           00553
         DC    X'03',C'HUIT'            D=172                           00554
         DC    X'03',C'NEUF'            D=177                           00555
         DC    X'03',C'ONZE'            D=182                           00556
         DC    X'05',C'TREIZE'          D=187                           00557
         DC    X'05',C'QUINZE'          D=194                           00558
         DC    X'07',C'DIX-HUIT'        D=201                           00559
         DC    X'07',C'DIX-NEUF'        D=210                           00560
         DC    X'04',C'VINGT'           D=219                           00561
         DC    X'05',C'TRENTE'          D=225                           00562
         DC    X'07',C'QUARANTE'        D=232                           00563
         DC    X'08',C'CINQUANTE'       D=241                           00564
         DC    X'07',C'SOIXANTE'        D=251                           00565
         DC    X'07',C'MILLIONS'        D=260                           00566
*                                                                       00567
FRFIXSNG EQU   ENGLISH+126              RE SINGULARS.                   00568
FRFIXPLR EQU   ENGLISH+6                RE PLURALS.                     00569
*                                                                       00570
*                                                                       00571
*                                                                       00572
* THE ITEMS THAT VARY BY CURRENCY ARE DEFINED BELOW.                    00573
*                                                                       00574
*  REGISTER 'RC' MUST BE LOADED WITH THE ADDRESS OF THE BEGINNING OF    00575
*  THE APPROPRIATE CURRENCY TABLE.                                      00576
*                                                                       00577
DOLLAR   DC    C'$'                                                     00578
         DC    X'05',C'DOLLAR'                                          00579
         DC    X'06',C'DOLLARS'                                         00580
         DC    X'06',C'CENT **    '                                     00581
         DC    X'07',C'CENTS **   '                                     00582
*                                                                       00583
POUNDDEC DC    C'<'                                                     00584
         DC    X'04',C'POUND '                                          00585
         DC    X'05',C'POUNDS '                                         00586
         DC    X'0A',C'NEWPENNY **'                                     00587
         DC    X'0A',C'NEWPENCE **'                                     00588
*                                                                       00589
POUNDLSD DC    C'<'                                                     00590
         DC    X'04',C'POUND '                                          00591
         DC    X'05',C'POUNDS '                                         00592
         DC    X'07',C'PENNY **   '                                     00593
         DC    X'07',C'PENCE **   '                                     00594
         DC    X'07',C'SHILLING'                                        00595
         DC    X'08',C'SHILLINGS'                                       00596
*                                                                       00597
         LTORG                                                          00598
*                                                                       00599
*                                                                       00600
* THE 3 DUMMY SECTINS WHICH FOLLOW ARE SET UP TO ALLOW EASY REFERENCE   00601
* TO ITEMS TO BE ACCESSED VIA REGISTERS 'RL', 'RC', AND 'RI'.           00602
*                                                                       00603
*                                                                       00604
LANGGO   DSECT                          USING 'RL'.                     00605
THOUSAND EQU   LANGGO+35                                                00606
HUNDRED  EQU   LANGGO+51                                                00607
ONLY     EQU   LANGGO+83                                                00608
AND      EQU   LANGGO+99                                                00609
DISP100S EQU   LANGGO+127                                               00610
MILLIONS EQU   LANGGO+260                                               00611
*                                                                       00612
CURRGO   DSECT                          USING 'RC'.                     00613
CURRSYMB EQU   CURRGO                                                   00614
MAJOR    EQU   CURRGO+1                                                 00615
MAJORS   EQU   CURRGO+8                                                 00616
MINOR    EQU   CURRGO+16                                                00617
MINORS   EQU   CURRGO+28                                                00618
INTER    EQU   CURRGO+40                                                00619
INTERS   EQU   CURRGO+49                                                00620
*                                                                       00621
* THE COMMENTS BESIDE THE DS ITEMS BELOW SHOW THE ORDER AND COBOL       00622
* PICTURES NEEDED IN A COBOL CALLING PROGRAMME.                         00623
*                                                                       00624
INANDOUT DSECT                          USING 'RI'.                     00625
INCODE   DS    CL1       PICTURE  X(1)  NEED ONLY THIS PARAMETER POINT. 00626
CHAROUT  DS    PL2       PICTURE  9(3)       COMPUTATIONAL-3            00627
CHARPLN  DS    PL2       PICTURE  9(3)       COMPUTATIONAL-3            00628
INDENT   DS    PL1       PICTURE  9(1)       COMPUTATIONAL-3            00629
AMOUNT   DS    PL6       PICTURE S9(9)V9(2)  COMPUTATIONAL-3            00630
LINESOUT DS    PL1       PICTURE S9(1)       COMPUTATIONAL-3            00631
NUMOUT   DS    CL16      PICTURE  X(16)                                 00632
ALFOUT   DS    CL1       PICTURE  X(THE NUMBER IN 'CHAROUT')            00633
*                                                                       00634
         END                                                            00635
